import com.android.build.OutputFile

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.appdistribution'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "ru.mg.myapplication"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 14
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

    }

    signingConfigs {
        release {

        }
    }
    firebaseAppDistribution {
        groups = "QA"

    }
    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        releaseQA {
            initWith release

            applicationIdSuffix '.releaseQA'
            versionNameSuffix '-RELEASE_QA'
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }
    flavorDimensions 'server'
    productFlavors {
        dev {
            applicationIdSuffix '.dev'

        }
        stage {
            applicationIdSuffix '.stage'

        }
        prod {

        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("dev") && variant.buildType.name == "release") {
            setIgnore(true)
        } else if (names.contains("stage") && variant.buildType.name == "release") {
            setIgnore(true)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    packagingOptions {
        exclude 'build-data.properties'
    }
    androidExtensions {
        experimental = true
    }
    dataBinding {
        enabled = true
    }

    splits {
        abi {
            enable gradle.startParameter.taskNames.any { it.contains("assembleProdRelease") }
            reset()
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            universalApk false
        }
    }
    ext.abiCodes = ['armeabi-v7a': 1, "arm64-v8a": 2, x86: 3, x86_64: 4]
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def baseAbiVersionCode = abiCodes.get(output.getFilter(OutputFile.ABI))
            if (baseAbiVersionCode != null) {
                output.versionCodeOverride =
                        baseAbiVersionCode * 1000 + variant.versionCode
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation project(path: ':client-core')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    implementation "com.github.tbruyelle:rxpermissions:$rxpermissions_version"

    implementation "androidx.multidex:multidex:$multidex_version"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    implementation "com.squareup.okhttp3:logging-interceptor:$logging_interceptor_version"

    implementation "com.google.crypto.tink:tink-android:$tink_version"

    implementation "com.hannesdorfmann:adapterdelegates4-kotlin-dsl-layoutcontainer:$adapter_delegates_version"
    implementation "com.hannesdorfmann:adapterdelegates4-kotlin-dsl-viewbinding:$adapter_delegates_version"

    testImplementation "org.mockito:mockito-core:$mockito_vertion"
    testImplementation "org.mockito:mockito-inline:$mockito_inline_vertion"

    implementation "com.facebook.shimmer:shimmer:$shimmer_version"

    implementation "com.google.firebase:firebase-analytics:$firebase_analytics_version"
    implementation "com.google.firebase:firebase-crashlytics:$firebase_crashlytics_version"
    implementation "com.google.firebase:firebase-messaging:${firebase_messaging_version}"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    implementation "com.squareup.picasso:picasso:$picasso_version"
    implementation "com.github.bmelnychuk:atv:${atv_version}"

    implementation "net.danlew:android.joda:${joda_version}"
    implementation("com.fatboyindustrial.gson-jodatime-serialisers:gson-jodatime-serialisers:${gson_jodatime_serialisers}") {
        exclude group: 'joda-time', module: 'joda-time'
    }
    implementation "com.daimajia.swipelayout:library:$swipe_layout_version"
    implementation "com.google.android:flexbox:$flexbox_version"

    implementation "androidx.viewpager2:viewpager2:$viewpager2_version"

    implementation "com.squareup.retrofit2:converter-scalars:$converter_scalars_version"
    implementation "com.appsflyer:af-android-sdk:$appflyer_version"
    implementation "com.android.installreferrer:installreferrer:$installreferrer_version"
    implementation "com.facebook.android:facebook-android-sdk:$facebook_sdk_version"
}

repositories {
    maven {
        url "http://maven.google.com/"
    }
}
